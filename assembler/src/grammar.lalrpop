use crate::ast::*;

use std::str::FromStr;

grammar;

// Skip whitespace and line comments
match {
    r"\s*" => { },
    r"--[^\n\r]*[\n\r]*" => { },
    _
}

pub Program: Vec<Item> = {
    <items:Item*> => items,
};

pub Item: Item = {
    <Label> => Item::Label(<>),
    <RealInstruction> => Item::Real(<>),
    <PseudoInstruction> => Item::Pseudo(<>),
};

ArithOp: ArithOp = {
    "add" => ArithOp::Add,
    "sub" => ArithOp::Sub,
    "and" => ArithOp::And,
    "or" => ArithOp::Or,
    "xor" => ArithOp::Xor,
    "shl" => ArithOp::Shl,
    "shr" => ArithOp::Shr,
    "sar" => ArithOp::Sar,
    "lts" => ArithOp::Lts,
    "ltu" => ArithOp::Ltu,
    "gts" => ArithOp::Gts,
    "gtu" => ArithOp::Gtu,
    "eq" => ArithOp::Eq,
    "ne" => ArithOp::Ne,
};

RealInstruction: RealInstruction = {
    <op:ArithOp> <rd:Register> "," <rs:Register> "," <rt:Register> => {
        RealInstruction::Arith { op, rd, rs, rt }
    },

    "not" <rd:Register> "," <rs: Register> => {
        RealInstruction::Arith { op: ArithOp::Not, rd, rs, rt: 0 }
    },

    "nop" => RealInstruction::Nop,

    "load" <rt:Register> "," <rs:Register> => {
        RealInstruction::Load { rt, rs, imm: 0 }
    },

    "load" <rt:Register> "," <s:Number> "(" <rs:Register> ")" =>? {
        i16::from_str(s)
            .map(|imm| RealInstruction::Load { rt, rs, imm })
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid 16-bit load offset"
            })
    },

    "store" <rs:Register> "," <rt:Register> => {
        RealInstruction::Store { rt, rs, imm: 0 }
    },

    "store" <s:Number> "(" <rs:Register> ")" "," <rt:Register> =>? {
        i16::from_str(s)
            .map(|imm| RealInstruction::Store { rt, rs, imm })
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid 16-bit store offset"
            })
    },
    
    "shi" <rs:Register> "," <s:Number> =>? {
        u16::from_str(s)
            .map(|imm| RealInstruction::Shi { rs, rt: rs, imm })
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid 16-bit set high value"
            })
    },

    "slo" <rs:Register> "," <s:Number> =>? {
        u16::from_str(s)
            .map(|imm| RealInstruction::Slo { rs, rt: rs, imm })
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid 16-bit set low value"
            })
    },

    "jr" <rs:Register> => {
        RealInstruction::Jr { rs }
    },
};

PseudoInstruction: PseudoInstruction = {
    "mov" <dst:Register> "," <s:Number> =>? {
        u32::from_str(s)
            .map(|imm| PseudoInstruction::Mov { dst, imm })
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid 26-bit address"
            })
    },

    "copy" <dst:Register> "," <src:Register> => {
        PseudoInstruction::Copy { dst, src }
    },

    "br" <rs:Register> "," <rt:Register> "," <s:r"[a-zA-Z_\.][a-zA-Z0-9_\.]*"> => {
        PseudoInstruction::Br { rs, rt, label: s.to_string() }
    },

    "jmp" <s:r"[a-zA-Z_\.][a-zA-Z0-9_\.]*"> => {
        PseudoInstruction::Jmp { label: s.to_string() }
    },

    "jar" <s:r"[a-zA-Z_\.][a-zA-Z0-9_\.]*"> => {
        PseudoInstruction::Jar { label: s.to_string() }
    },
};

Number: &'input str = {
    <s:r"-?[0-9]+"> => s,
};

Register: u8 = {
    <s:r"\$([0-9]+)"> =>? {
        let num_str = &s[1..]; // skip the '$'
        let val = u8::from_str(num_str)
            .map_err(|_| lalrpop_util::ParseError::User {
                error: "Invalid register number"
            })?;
        if val > 31 {
            Err(lalrpop_util::ParseError::User {
                error: "Register number must be between 0 and 31"
            })
        } else {
            Ok(val)
        }
    },
};

Label: String = {
    <s:r"[a-zA-Z_\.][a-zA-Z0-9_\.]*"> ":" => s.to_string(),
};